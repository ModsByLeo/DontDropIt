plugins {
    id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
    accessWidener "src/main/resources/dontdropit.accesswidener"
}

repositories {
    maven { url "https://dl.bintray.com/adudecalledleo/mcmods.fabric" }
    maven { url "https://dl.bintray.com/shedaniel/cloth-config-2" }
    maven { url "https://maven.siphalor.de/" }
}

def depImpl(String id, boolean include = false) {
    dependencies.modImplementation (id) {
        exclude group: "net.fabricmc.fabric-api"
    }
    if (include)
        dependencies.include (id) {
            exclude group: "net.fabricmc.fabric-api"
        }
}

def depRuntime(String id) {
    dependencies.modRuntime(id) {
        exclude group: "net.fabricmc.fabric-api"
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
    depImpl("adudecalledleo.mcmods.fabric:lionutils:${project.lionutils_version}", true)
    depImpl("me.shedaniel.cloth:config-2:${project.cloth_config_version}", true)
    depImpl("me.sargunvohra.mcmods:autoconfig1u:${project.autoconfig1u_version}", true)
    depImpl("de.siphalor:amecsapi-1.16:1+", true)
    depImpl "io.github.prospector:modmenu:${project.modmenu_version}"
}

if (Boolean.valueOf(project.use_runtime_deps)) {
    repositories {
        maven { url "https://jitpack.io" }
    }

    dependencies {
        // DataBreaker applies a number of patches to eliminate the loading of data fixers
        // This greatly reduces the amount of time needed to start the game, but is generally unsafe in production
        depRuntime "com.github.SuperCoder7979:databreaker:${project.databreaker_version}"
        // JellySquid's elemental optimizations
        depRuntime "com.github.jellysquid3:lithium-fabric:${project.lithium_version}"
        depRuntime "com.github.jellysquid3:phosphor-fabric:${project.phosphor_version}"
        depRuntime "com.github.jellysquid3:sodium-fabric:${project.sodium_version}"

        // ...don't judge me, this fixes a weird-ass problem with Lithium
        runtime('net.fabricmc:sponge-mixin:0.8+build.18') {
            force = true
        }
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
