plugins {
    id 'fabric-loom' version '0.9-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/dontdropit.accesswidener")
}

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/" }
    maven { url "https://maven.siphalor.de/" }
}

def depImpl(String id, boolean include = false) {
    dependencies.modImplementation (id) {
        exclude group: "net.fabricmc.fabric-api"
    }
    if (include)
        dependencies.include (id) {
            exclude group: "net.fabricmc.fabric-api"
        }
}

def depRuntime(String id) {
    dependencies.modRuntime(id) {
        exclude group: "net.fabricmc.fabric-api"
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
    depImpl("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}", true)
    // Amecs API automatically brings in (a broken version of) NMUK, which we don't need
    include(modImplementation("de.siphalor:amecsapi-1.17:1+") {
        exclude group: "de.siphalor", module: "nmuk-1.17"
    })
    depImpl "com.terraformersmc:modmenu:${project.modmenu_version}"
}

if (Boolean.valueOf(project.use_runtime_deps)) {
    repositories {
        maven { url "https://jitpack.io" }
    }

    dependencies {
        // DataBreaker applies a number of patches to eliminate the loading of data fixers
        // This greatly reduces the amount of time needed to start the game, but is generally unsafe in production
        depRuntime "com.github.SuperCoder7979:databreaker:${project.databreaker_version}"
        // JellySquid's elemental optimizations
        depRuntime "com.github.jellysquid3:lithium-fabric:${project.lithium_version}"
        depRuntime "com.github.jellysquid3:phosphor-fabric:${project.phosphor_version}"
        depRuntime "com.github.jellysquid3:sodium-fabric:${project.sodium_version}"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // Minecraft 1.17 (21w19a) upwards uses Java 16.
    it.options.release = 16
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}
